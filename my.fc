options( bank:0 );

use * from common;
use en;
use math;

const STATE_STAND = 0;
const STATE_JUMP = 2;
const STATE_LADDER = 3; // はしご
const STATE_ON_ENEMY = 4;
const STATE_DIE = 255;

var x:int, y:int;
var my_anim:int;
var my_vx:sint;
var my_vy:sint;
var vy:sint, vx:sint;
var dir:int;
var state:int;
var on_idx:int;
var on_x:int;
var on_y:int;
var jump_pow:int;
var prev_in_water:int;
var items:int[64];

private:

function get_cell(x:int, y:int):int
{
	if( y > 240 ){ return 0; }
	var c = bg.TYPE[bg.data[(y/16)*16 + x/16]];
	if( c & bg.TYPE_WALL ){
		if( c & bg.TYPE_FLOOR ){
			return (y % 16 < 8);
		}else{
			return 1;
		}
	}else{
		return 0;
	}
}

function get_cell_type(x:int, y:int):int
{
	return bg.TYPE[bg.data[(y/16)*16 + x/16]];
}

function check_on_enemy():int
{
	var i:int;
	for( i, 0, en.MAX ){
		switch( en.type[i] ){
		case en.TYPE_ELEVATOR:
			var size:int;
			if( en.p3[0] == 1 ){ size = 14; }else{ size = 20; }
			if( math.abs(x-en.px[i])<size && y>=(en.py[i]-18) && y < (en.py[i]-10) ){
				y = en.py[i] - 16;
				return i;
			}
		case en.TYPE_SLIME:
			if( items[fs.ITEM_ID_SANDAL] && math.abs(x-en.px[i])<14 && y>=(en.py[i]-12) && y < (en.py[i]-2) ){
				y = en.py[i] - 10;
				return i;
			}
		case en.TYPE_FROG:
			if( items[fs.ITEM_ID_BOOTS] && math.abs(x-en.px[i])<14 && y>=(en.py[i]-12) && y < (en.py[i]-2) ){
				y = en.py[i] - 10;
				return i;
			}
		}
	}
	return -1;
}

public function process():void
{
	// 水の中の処理
	var bi:int;
	var in_water = get_cell_type(x,y) & bg.TYPE_WATER;
	if( prev_in_water ^ in_water ){
		// 水に飛び込んだ飛沫を表示
		bi = en.new();
		en.type[bi] = en.TYPE_SPLASH;
		en.px[bi] = x;
		en.py[bi] = (y+8)/16*16+8;
		en.p1[bi] = 8;
	}
	if( in_water ){
		if( anim % 64 == 0 ){
			bi = en.new();
			en.type[bi] = en.TYPE_BUBBLE;
			en.px[bi] = x;
			en.py[bi] = y;
		}
		if( anim % 2 ){	return; }
	}
	prev_in_water = in_water;
	var limited_water = in_water && (items[fs.ITEM_ID_WEIGHT]==0);
	
	vx = 0;
	my_anim += 1;
	
	// 縦移動
	switch( state ){
	case STATE_STAND, STATE_ON_ENEMY:
		// 地上
		
		// 左右移動
		if( pad.right ){ my_vx += 4; dir = 0; }
		if( pad.left ){ my_vx -= 4; dir = 1; }
		if( !pad.right && !pad.left ){ my_vx = 0; }
		if( my_vx > 16 ){ my_vx = 16; }
		if( my_vx < -16 ){ my_vx = -16; }
		vx = (my_vx+giff)/16;

		var evx = 0, evy = 0;
		if( state == STATE_ON_ENEMY ){
			// 敵に乗ってる
			var ei = on_idx;
			evx += en.px[ei] - on_x;
			evy += en.py[ei] - on_y;
			on_x = en.px[ei];
			on_y = en.py[ei];
		}
		
		x += vx + evx;

		if( anim % 32 == 0 && vx != 0 && items[fs.ITEM_ID_GROBE]){
			var block_x = x + math.sign(vx)*9;
			if( bg.cell( block_x, y ) == 56 ){
				bi = en.new();
				if( bi != -1 ){
					en.type[bi] = en.TYPE_BLOCK;
					en.px[bi] = block_x/16*16+8;
					en.py[bi] = y/16*16+8;
					en.p1[bi] = math.sign(vx);
				}
			}
		}

		if( get_cell(x-4,y+6) || get_cell(x+4,y+6) || get_cell(x-4,y-4) || get_cell(x+4,y-4) ){
			x -= vx + evx;
		}
		
		if( pad.up && get_cell_type(x,y) & bg.TYPE_LADDER ){
			// はしごに登り始める
			state = STATE_LADDER;
		}elsif( pad.down && get_cell_type(x,y+8) & bg.TYPE_LADDER ){
			// はしごに下り始める
			state = STATE_LADDER;
			y += 8;
		}else{
			// 地上にいる
			if( state == STATE_STAND ){
				if( !get_cell(x-4,y+8) && !get_cell(x+4,y+8) ){
					// 床から落ちた
					y += 1;
					state = STATE_JUMP;
				}
			}else{
				// 敵に乗ってる
				if( check_on_enemy() != on_idx ){
					y += 1;
					state = STATE_JUMP;
				}
			}
			if( pad.pushed & (pad.A|pad.UP) ){
				// ジャンプした！
				y -= 1;
				vy = -36;
				jump_pow = 10;
				state = STATE_JUMP;
			}
		}
	case STATE_JUMP: // ジャンプ中
		// 空中にいる
		var vvx = 1;
		if( jump_pow ){ vvx = 4; }
		if( pad.right ){ my_vx += vvx; dir = 0; }
		if( pad.left ){ my_vx -= vvx; dir = 1; }
		if( !pad.left && !pad.right ){ my_vx -= math.sign(my_vx); }
		if( my_vx > 16 ){ my_vx = 16; }
		if( my_vx < -16 ){ my_vx = -16; }
		x += (my_vx+giff)/16;
		if( get_cell(x-4,y+8) || get_cell(x+4,y+8) || get_cell(x-4,y-4) || get_cell(x+4,y-4) ){
			x -= (my_vx+giff)/16;
		}
		if( limited_water ){ vy -= 4; }
		

		vy += 2;
		y += vy / 16;

		if( vy < 0 ){
			if( limited_water && vy < -16 ){ vy = -16; } // 水の中の制限
			//上昇中
			if( get_cell(x-4,y-4) || get_cell(x+4,y-4) ){
				y -= vy / 16;
				vy = 0;
				jump_pow = 0;
			}else{
				if( jump_pow ){
					jump_pow -= 1;
					if( pad.cur & (pad.A|pad.UP) ){
						vy -= 1;
					}else{
						vy += 22;
						jump_pow = 0;
					}
				}
			}
		}else{
			// 下降中
			if( vy > 64 ){ vy = 64; }
			if( limited_water && vy > 32 ){ vy = 32; } // 水の中の制限
			
			if( get_cell(x-4,y+8) || get_cell(x+4,y+8) ){
				// 着地
				vy = 0;
				y = (y+8)/16*16 - 8;
				state = STATE_STAND;
			}else{
				on_idx = check_on_enemy();
				if( on_idx != -1 ){
					// 敵に乗った!
					state = STATE_ON_ENEMY;
					vy = 0;
					on_x = en.px[on_idx];
					on_y = en.py[on_idx];
				}
			}
		}
		
		if( in_water && (pad.cur & (pad.A|pad.UP)) ){
			vy = -28;
			jump_pow = 10;
			if( vy <= -28 ){ vy = -28; }
		}
		
	case STATE_LADDER: // はしご
		x = (x/16)*16+8;
		if( (pad.right | pad.left ) & !get_cell(x,y) ){
			state = STATE_JUMP;
		}elsif( pad.up ){
			// 登る
			if( get_cell_type(x,y-4) != bg.TYPE_WALL ){
				if( get_cell_type(x,y) & bg.TYPE_LADDER ){
					y -= 1;
				}else{
					y -= 8;
					state = STATE_STAND;
				}
			}
		}elsif( pad.down ){
			// 降りる
			if( !(get_cell_type(x,y+7) & bg.TYPE_LADDER) ){
				y -= 1;
				state = STATE_STAND;
			}else{
				y += 1;
			}
		}
	}

	// 刺
	var c = get_cell_type(x,y);
	if( c & bg.TYPE_SPIKE ){
		ppu.wait(100);
		state = STATE_DIE;
	}

}

public function draw():void
{
	var a:int;
	switch( state ){
	case STATE_STAND, STATE_ON_ENEMY, STATE_DIE:
		a = ((my_anim/16)%2)*4;
		if( vx == 0 ){ a = 0; }
		gr_sprite2( x, y, a, 64*dir );
	case STATE_JUMP:
		if( vy > 128 ){
			gr_sprite2( x, y, 8, 64*dir );
		}else{
			gr_sprite2( x, y, 12, 64*dir );
		}
	case STATE_LADDER:
		a = (my_anim/16)%2;
		gr_sprite2( x, y, 16, 64*a );
	}
}

public function on_scroll(dx:int, dy:int):void
{
	x += dx;
	y += dy;
	draw();
}

